EntityModel:
    'Database' '{'
        database=Database
    '}'
    entities+=Entity
;

Database:
    'DB driver: ' driver=Driver ','
    'DB name:' name=NamePattern
    (',' credentials=Credentials)?
;

Credentials:
    'DB username:' username=NamePattern ','
    'DB password:' password=NamePattern
;

Entity:
    (class_modifier=ClassModifier)? 'class' name=NamePattern '{'
        properties+=Property
        'Constructors' '{' 
            constructors+=Constructor[',']
        '}'
        'Methods' '{'
            (methods+=Method[','])?
        '}'
        'toString:' to_string=YesNoValue
    '}'
;

Driver:
    'postgresql' | 'mysql' | 'sqlserver' | 'oracle'
;

ClassModifier:
    'public' | 'abstract' | 'final' | ''
;

Property:
    name=NamePattern ':' (constant?=Constant)? property_type=[Type] (list_type=[ListType])? (relationship=Relationship)? (encapsulation=Encapsulation)? (property_value=Value)? ';'
;

Constructor:
    empty_constructor?='empty' | default_constructor?='default' | '[' property_list=[Property] (', ' property_list=[Property])* ']'
;

Method:
    method_modifier=MethodMofidier (static?='static')? method_declaration=MethodDeclaration name=NamePattern '(' (method_properties=MethodReferences)? ')'
;

MethodReferences:
    property_list=[Property] (', ' property_list=[Property])* | property_list=[Entity] (', ' property_list=[Entity])*
;

MethodMofidier:
    'public' | 'private' | 'protected' | 'final' | ''
;

MethodDeclaration:
    VoidMethodType | ListMethodType | RestMethodType
;

VoidMethodType:
    method_type='void'
;

ListMethodType:
    list_type=[ListType] '(' method_type=[Type] ')'
;

RestMethodType:
    method_type=[Type]
;

Type:
    IDType | DataType | DateType | Entity
;

IDType:
    type=IDTypes
;

DataType:
    PrimitiveDataType | WrapperDataType | OtherDataType
;

DateType:
    type=DateTypes
;

PrimitiveDataType:
    type=PrimitiveDataTypes
;

WrapperDataType:
    type=WrapperDataTypes
;

OtherDataType:
    type=OtherDataTypes
;

ListType:
    type=ListTypes
;

IDTypes:
    'id' | 'identifier' | 'uniqueId' | 'key' | 'primaryKey'
;

PrimitiveDataTypes:
    'byte' | 'short' | 'char' | 'int' | 'float' | 'long' | 'double' | 'boolean'
;

WrapperDataTypes:
    'Byte' | 'Short' | 'Character' | 'Integer' | 'Float' | 'Long' | 'Double' | 'Boolean'
;

OtherDataTypes:
    'str' | 'string' | 'String'
;

DateTypes:
    'date' | 'time' | 'datetime'
;

ListTypes:
    'array' | 'linked' | 'hashmap' | 'hashset' | 'treemap' | 'list'
;

YesNoValue:
    'yes' | 'no'
;

Constant:
    'constant' | 'const'
;

Relationship:
    type=Relationships owner?='(+)'
;

Relationships:
    '1..1' | '1..*' | '*..1' | '*..*'
;

Encapsulation:
    '(' EncapsulationOptions ')'
;

EncapsulationOptions:
    GetterSetterOption | SetterGetterOption
;

GetterSetterOption:
    getter?=AccessorMethod (', ' setter?=MutatorMethod)?
;

SetterGetterOption:
    setter?=MutatorMethod (',' getter?=AccessorMethod)?
;

AccessorMethod:
    'getter' | 'get'
;

MutatorMethod:
    'setter' | 'set'
;

Value:
    '=' value=ValuePattern
;

NamePattern:
    /[^\s:(\n]*\b/
;

ValuePattern:
    /[^\/;\r\n]+/
;

Comment:
    /\/\/.*$/
;
